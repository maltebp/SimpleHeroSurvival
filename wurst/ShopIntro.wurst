package ShopIntro
import GladiatorSystem
import Scene
import GameSettings
import Abilities
import TimedSpecialEffects
import StringUtils
import ReadyHandler
import Round



public function player.startShopIntro(GladiatorType gladiatorType)
    
    let scene = new Scene()
    
    scene.do() p ->
        p.resetToGameCamera(0)
        p.setCameraPosition(SHOP_SPAWN)
        if LOCK_CAM_BOUNDS
            p.setCameraBoundsToRect(gg_rct_Shop_CamBounds)

    scene
    ..wait(1)
    ..do( p -> p.fadeIn(1) )
    ..wait(1)
    ..do( p -> p.createGladiator(gladiatorType))
    ..wait(1.25)
    ..do( p -> p.getGladiator().getSpawnSound().play() )
    ..wait(2)
    ..do(p -> p.selectSingle(p.getGladiator().getUnit()))
    ..wait(3)
    ..do() p ->
        SND_MESSAGE.playForPlayer(p)
        p.print("Prepare yourself now, and when you're ready, press '{0}' to start the round.".format("escape".withColor(COLOR_HIGHLIGHT)),5)
    scene
    ..wait(8)
    ..do() p ->
        Round.printNextRound(p)
        p.enableReadyButton()
    scene.play(this)
    


function player.createGladiator(GladiatorType gladiatorType)
    
    let gladiator = this.newGladiator(gladiatorType, SHOP_SPAWN, angle(3*PIHALF))

    // Unlock All Initial Abilities
    for i=0 to UNLOCK_ABILITY_ROUNDS.length-1
        if( UNLOCK_ABILITY_ROUNDS[i] == 0 )
            gladiator.unlockNextAbility()
    
    addEffect(Abilities.massTeleportCaster, gladiator.getUnit().getPos() )
    ..setDuration(3)

    // TODO: Remove or implement this
    // if PREPAREHERO
    //     for i = 0 to 4
    //         hero.addItemById(STARTINGITEMS[i])
