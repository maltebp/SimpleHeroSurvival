package Transitions

import SoundUtils
import TimerUtils
import Camera
import FadeAndFilters
import initlater Game
import initlater Round
import Music
import TimedSpecialEffects


SoundDefinition snd_EscapePress = new SoundDefinition(Sounds.bigButtonClick, false)
SoundDefinition snd_RoundWon = new SoundDefinition(Sounds.questCompleted, false)
SoundDefinition snd_RoundLost = new SoundDefinition(Sounds.questFailed, false)
SoundDefinition snd_RoundInfo = new SoundDefinition(Sounds.rescue, false)
SoundDefinition snd_RoundComplete = new SoundDefinition(Sounds.goodJob, false)


boolean hasWon = false 

effect teleportEffect


init
    
// -------------------------------------------------------------------------------------------

public function transitionToShop(boolean won)
    
    hasWon = won
    stopMusic()

    if hasWon
        hero.setInvulnerable(true)
        

    getTimer().start(3, function toShop_PlaySound )    


function toShop_PlaySound()

    if hasWon
        snd_RoundWon.play()
    else
        snd_RoundLost.play()

    GetExpiredTimer().start(4, function toShop_StartFade)


function toShop_StartFade()
    GetExpiredTimer().release()

    if hero.isAlive()
        hero.pause()

    new Fade
    ..setStartDelay(0)
    ..setMidDelay(1)
    ..setEndDelay(1)
    ..onFadeOut(function toShop_OnFadeOut )
    ..onFadeIn(function toShop_OnFadeIn )
    ..start(FADEOUTIN)


function toShop_OnFadeOut()
    Player(0).setCameraBoundsToRect(gg_rct_Shop_CamBounds)
    Player(0).setCameraPosition(HEROSPAWN_SHOP)
    Round.clearCurrentRound()
    

function toShop_OnFadeIn()

    if hasWon
        hero.setPos(HEROSPAWN_SHOP)
        addEffect("Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl", HEROSPAWN_SHOP)
        ..setDuration(4)
        
    else
        hero.revive(HEROSPAWN_SHOP, true)
        gladiator.getSoundOnRespawn().play()
        hero.pause()

    playMusic(Sounds.orcX1)
    hero.setHP(hero.getMaxHP())
    hero.setMana(hero.getMaxMana())
    
    hero.setInvulnerable(false)

    getTimer().start(3, function toShop_RoundReview )


function toShop_RoundReview()

    ClearTextMessages()

    hero.unpause()

    if hasWon and Round.getCurrentRoundNumber() == Round.getNumberOfRounds()
        playMusic(Sounds.heroicVictory)
        print("Congratulations, you've completed all rounds!")
        GetExpiredTimer().start(5, function gameVictory)

    else if hasWon
        printTimed("|cff00cc66"+"Round "+Round.getCurrentRoundNumber().toString()+" completed!"+"|r",7 )
        snd_RoundComplete.play()
        GetExpiredTimer().start(4, function toShop_Reward )

    else
        printTimed("|cffff3300"+"Round "+Round.getCurrentRoundNumber().toString()+" failed!"+"|r",7 )
        snd_RoundLost.play()
        GetExpiredTimer().start(4, function toShop_TryAgain )


function toShop_Reward()
    Round.giveReward()
    GetExpiredTimer().start(6, function toShop_RewardItems )
    

function toShop_RewardItems()
    boolean anyItemsToReward = Round.giveItemReward()
    if anyItemsToReward
        GetExpiredTimer().start(6, function toShop_RewardAbilities )
    else
        GetExpiredTimer().start(0, function toShop_RewardAbilities )


function toShop_RewardAbilities()
    boolean anyAbilitiesToReward = Round.getCurrentRound().giveAbilityReward()

    if anyAbilitiesToReward
        GetExpiredTimer().start(6, function toShop_NextRound )
    else
        GetExpiredTimer().start(0, function toShop_NextRound )

        
function toShop_NextRound()
    Round.setNextRound()
    ClearTextMessages()
    Round.printNextRound()
    Round.setRoundReady()
    GetExpiredTimer().release()

function toShop_TryAgain()
    printTimed(" ",7)
    printTimed("Try again", 7)
    Round.setRoundReady()



function gameVictory()
    CustomVictoryBJ(Player(0), true, false)




//-------------------------------------------------------------------------------------------------------------------


public function transitionToArena()
    hero.pause()
    getTimer().start(2, function toArena_StartingFade )


function toArena_StartingFade()
    GetExpiredTimer().release()
    new Fade
    ..setStartDelay(0)
    ..onFadeOut(function toArena_OnFadeOut )
    ..onFadeIn(function toArena_OnFadeIn )
    ..setEndDelay(1)
    ..start(FADEOUTIN)


function toArena_OnFadeOut()
    ClearTextMessages()
    Player(0).setCameraBoundsToRect(gg_rct_Arena_CamBounds)
    Player(0).setCameraPosition(HEROSPAWN_ARENA)
    Round.getCurrentRound().prepare()


function toArena_OnFadeIn()
    hero.setPos(HEROSPAWN_ARENA)
    addEffect("Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl", HEROSPAWN_ARENA)
    ..setDuration(4)

    getTimer().start(3, function toArena_roundInfo )

function toArena_roundInfo()
    snd_RoundInfo.play()
    printTimed(Round.getCurrentRound().getTitle(), 6)
    getTimer().start(4, function toArena_StartRound )

function toArena_StartRound()
    GetExpiredTimer().release()
    
    hero.unpause()
    Round.getCurrentRound().initialize()
    
    