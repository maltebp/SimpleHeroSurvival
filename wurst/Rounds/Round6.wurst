package Round6

import public initlater Round5

import TimerUtils
import Orders
import RegisterEvents
import GroundEffect
import GroupUtils
import TimedSpecialEffects
import AutoCastAbility

import LinkedList

LinkedList<Flame> flames = new LinkedList<Flame>

init
//=========================================================================================================================================================
//  Flame Elementals

    round = new Round
    ..setReward(70, 4)
    ..setTitle("Through the Fire and Flames")
    ..setDescription("It's gettin' hot in here - should you stand in the fire?")
    ..setHealthOrbSpawn(90, 60, 0.1, 15, 0.02)


    // Spawner 1

    round.addSpawner(new UnitSpawner('n00B', ENEMYPLAYER, vec2(-639,1025), 15))
    ..setStartDelay(4)
    ..setFrequencyDeviation(0.1)
    ..setRandomSpawn(gg_rct_Arena_Spawn1)
    ..setNearestTargetOnSpawn(GetPlayableMapRect())
    ..setSpawnCount(4)
    ..setSpawnEffect("Abilities\\Spells\\Demon\\DarkPortal\\DarkPortalTarget.mdl")

    //Spawner 2

    round.addSpawner(new UnitSpawner('n00C', ENEMYPLAYER, vec2(527,1025), 12))
    ..setStartDelay(5)
    ..setFrequencyDeviation(0.03)
    ..setRandomSpawn(gg_rct_Arena_Spawn1)
    ..setNearestTargetOnSpawn(GetPlayableMapRect())
    ..setSpawnCount(6)
    ..setSpawnEffect("Abilities\\Spells\\Demon\\DarkPortal\\DarkPortalTarget.mdl")
    
    // //Spawner 2
    // func = (UnitSpawner spawner) -> begin
    // end

    // round.addSpawner(new UnitSpawner('u002', ENEMYPLAYER, vec2(527,1025), 10))
    // ..setStartDelay(60)
    // ..setFrequencyDeviation(0.03)
    // ..setRandomSpawn(gg_rct_Arena_Spawn1)
    // ..setNearestTargetOnSpawn(GetPlayableMapRect())
    // ..setSpawnCount(3)
    // ..setSpawnEffect("Abilities\\Spells\\Undead\\CarrionSwarm\\CarrionSwarmDamage.mdl")
    // ..setCodeOnSpawn(func)







class Flame extends GroundEffect

    private static group targetsHit = CreateGroup()

    private unit caster
    private real range

    construct(vec2 pos, unit caster)
        super(pos, "Abilities\\Spells\\Human\\FlameStrike\\FlameStrikeEmbers.mdl")

        this.caster = caster
        baseSfxScale = 2

        if caster.getTypeId() == 'n00B'
            range = 200
            addLayer(0, 1)
            addLayer(110, 5)
            addLayer(140, 5)
            addLayer(180, 7)

        else
            range = 135
            addLayer(0, 1)
            addLayer(90, 6)

        flames.add(this)


    override function doEffect()
        
        ENUM_GROUP.enumUnitsInRange(pos, range)
        
        for target from ENUM_GROUP
            if target.isAlive() and target.getOwner().isEnemyOf(ENEMYPLAYER) and not targetsHit.contains(target)
                target.addEffect("Abilities\\Spells\\Items\\AIfb\\AIfbSpecialArt.mdl", "chest")
                caster.damageTarget(target, 75)
                targetsHit.addUnitTimed(target, 0.99)


function lavaSpawnDies()

    if GetDyingUnit().getTypeId() == 'n00C'
        addEffect("Objects\\Spawnmodels\\Human\\SmallFlameSpawn\\SmallFlameSpawn.mdl", GetDyingUnit().getPos() )
        ..setTimeScale(1.5)
        ..setScale(0.5)
        ..setDuration(5)
    
        new Flame(GetDyingUnit().getPos(), GetDyingUnit())
        

class FlameStrike
    unit caster
    vec2 pos

    construct(unit caster, vec2 pos)

        this.caster = caster
        this.pos = pos

        

        getTimer()
        ..setData(this castTo int)
        ..start(2, function createFlames )

    private static function createFlames()
        timer expiredTimer = GetExpiredTimer()
        thistype instance = expiredTimer.getData() castTo thistype
        expiredTimer
        ..pause()
        ..release()

        if instance.caster != null and instance.caster.isAlive()
            addEffect("Abilities\\Spells\\Human\\FlameStrike\\FlameStrike1.mdl", instance.pos)
            ..setDuration(10)
            ..setScale(2)

            new Flame(vec2(GetSpellTargetX(), GetSpellTargetY()), GetSpellAbilityUnit())


function flameStrikeCast()
    new FlameStrike(GetSpellTargetUnit(), vec2(GetSpellTargetX(), GetSpellTargetY()))

init
    registerSpellEffectEvent('A010', function flameStrikeCast )
    addAutoCastAbilityToUnitType('n00B', 'A010', 10, 15, 600, Orders.flamestrike, TargetType.ENEMY_POINT)
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function lavaSpawnDies )
    
    
        


        


    

    
