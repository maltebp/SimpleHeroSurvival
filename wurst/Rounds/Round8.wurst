package Round8


//=========================================================================================================================================================
// Rock Giants

import public Round7

import RegisterEvents
import AutoCastAbility
import UnitVisualsUtils
import OnUnitTypeEnter
import DeathSpawn
import Shockwave
import ClosureTimers
import EarthquakeNoChannel
import Preloader


// ================================================================================================================================
// Settings

let ROCKGIANT = 'e000'
let SHARDLING = 'n00O'

//------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

let SHARDLINGS_SPAWNNUMBER = 4
let SHOCKWAVE_STARTCOOLDOWN = 5.
let SHOCKWAVE_COOLDOWN = 15.
let SHOCKWAVE_DMG_FACTOR  = 15.
let SHOCKWAVE_SPEED = 800.
let SHOCKWAVE_WIDTH = 175.
let SHOCKWAVE_CASTDELAY = 2.

//------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

let EARTHQUAKE_DURATION = 30.
let EARTHQUAKE_COOLDOWN = 120.
let EARTHQUAKE_STARTCOOLDOWN = 10.
let EARTHQUAKE_SLOWFACTOR = 0.4
let EARTHQUAKE_RANGE = 250.
// For other earthquake adjustments see the dummy spell

constant let snd_Shockwave = new SoundDefinition(Sounds.mountainGiantPissed1, false)

Round round = null


//=========================================================================================================================================================
//  Setup

init

    round = new Round()
    ..setReward(30, 3)
    ..addItemRewards(ITEM_STONEDOLL, ITEM_GIANTSSKIN)
    ..addArtifactRewards(1)
    ..setTitle("Rock Giants")
    ..setDescription("It's probably best if you clean up the rubbles you smash them into.")
    ..setHealthOrbSpawn(125, 75, 0.1, 30, 0.02)
    ..setCodeOnClear() r ->
        Earthquake.clearAll()

    // Spawner 1
    round.addSpawner(new UnitSpawner(ROCKGIANT, ENEMY, vec2(-639,1025), 15))
    ..setStartDelay(3)
    ..setFrequencyDeviation(0.1)
    ..setRandomSpawn(gg_rct_Arena_Spawn1)
    ..setNearestTargetOnSpawn(GetPlayableMapRect())
    ..setSoundOnSpawn(Sounds.mountainGiantYes3)
    ..setSpawnEffect("Objects\\Spawnmodels\\Undead\\ImpaleTargetDust\\ImpaleTargetDust.mdl")
    ..setSpawnCount(8)

    // Spawner 2
    round.addSpawner(new UnitSpawner(ROCKGIANT, ENEMY, vec2(-639,1025), 30))
    ..setStartDelay(50)
    ..setFrequencyDeviation(0.1)
    ..setRandomSpawn(gg_rct_Arena_Spawn1)
    ..setNearestTargetOnSpawn(GetPlayableMapRect())
    ..setSoundOnSpawn(Sounds.mountainGiantYes3)
    ..setSpawnEffect("Objects\\Spawnmodels\\Undead\\ImpaleTargetDust\\ImpaleTargetDust.mdl")
    ..setSpawnCount(2)

    // Other setup

    UnitVisuals.addUnitType(ROCKGIANT)
    ..setPlayerColor(PLAYER_COLOR_LIGHT_GRAY)

    UnitVisuals.addUnitType(SHARDLING)
    ..setPlayerColor(PLAYER_COLOR_LIGHT_GRAY)

    onUnitTypeEnter(ROCKGIANT) rockGiant ->
        new DeathSpawnData(rockGiant, SHARDLING, SHARDLINGS_SPAWNNUMBER)
        ..setEffectOnSpawn("Objects\\Spawnmodels\\Undead\\ImpaleTargetDust\\ImpaleTargetDust.mdl")
        ..setCodeOnUnitSpawn() spawnedUnit ->
            rockGiant.getSpawner().addSummonedUnit(spawnedUnit)

    addAutoCastAbilityToUnitType(ROCKGIANT, new AutoCastAoePoint('A01U', Orders.carrionswarm, TargetType.ENEMY_POINT, 600, SHOCKWAVE_STARTCOOLDOWN, SHOCKWAVE_COOLDOWN))
    
    addAutoCastAbilityToUnitType(ROCKGIANT, new AutoCastAoePoint('A01W', Orders.dispel, TargetType.ENEMY_POINT, 800, EARTHQUAKE_STARTCOOLDOWN, EARTHQUAKE_COOLDOWN))
    

    registerSpellEffectEvent('A01U', function shockWaveEffect)

    registerSpellEffectEvent('A01W', function earthquakeCast)


    preloadAbility('A01U')
    preloadAbility('A01W')


//================================================================================================================================================================================================
// STATS SCALING

    round.setStatScalingCallback() (unit u) ->
        let id = u.getTypeId()
        if( id == ROCKGIANT or id == SHARDLING)
            round.scaleUnitStats(u)
            u.adjustToDifficulty()

//=========================================================================================================================================================
// 
//

function earthquakeCast()
    new Earthquake(GetSpellAbilityUnit(), vec2(GetSpellTargetX(), GetSpellTargetY()), EARTHQUAKE_DURATION, EARTHQUAKE_RANGE, EARTHQUAKE_SLOWFACTOR)


function shockWaveEffect()
    let caster = GetSpellAbilityUnit()
    let targetPos = vec2(GetSpellTargetX(), GetSpellTargetY())

    caster.setAnimation("spell")
    snd_Shockwave.play() 

    doAfter(SHOCKWAVE_CASTDELAY) ->
        if caster.isAlive()
            caster.setAnimation("attack")
            caster.castShockwave(targetPos, caster.getPower() * SHOCKWAVE_DMG_FACTOR, 2500, SHOCKWAVE_WIDTH, SHOCKWAVE_SPEED)

   


    