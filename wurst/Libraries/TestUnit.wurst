package TestUnit
import ObjectIdGenerator
import UnitObjEditing
import UnitIds
import LinkedList
import UtilCommands

@configurable
let DEFAULT_HP = 500.

@configurable
let DEFAULT_ARMOR = 0.

LinkedList<int> UNIT_IDS = new LinkedList<int> 

let UNITID_FEMALE_1 = compiletime(UNIT_ID_GEN.next())
let UNITID_FEMALE_2 = compiletime(UNIT_ID_GEN.next())

let UNITID_MALE_1 = compiletime(UNIT_ID_GEN.next())
let UNITID_MALE_2 = compiletime(UNIT_ID_GEN.next())


/** Spawns a test unit, which won't attack anythin */
public function createTestUnit( vec2 pos) returns unit
    return createTestUnit(pos, DEFAULT_HP, DEFAULT_ARMOR)

    
public function createTestUnit(vec2 pos, real hp, real armor) returns unit
    let u = createUnit(Player(PLAYER_NEUTRAL_AGGRESSIVE), UNIT_IDS.get(GetRandomInt(0,UNIT_IDS.size()-1)), pos, angle(GetRandomReal(0, PI2)))
    ..setMaxHP(hp.toInt(),true)
    ..setArmor(armor)
    return u


public function unit.isTestUnit() returns boolean
    return UNIT_IDS.has(this.getTypeId())
    

init
    UNIT_IDS.add(
        UNITID_FEMALE_1,
        UNITID_FEMALE_2,
        UNITID_MALE_1,
        UNITID_MALE_2
    )

@compiletime
function defineUnit() 
    
    new UnitDefinition(UNITID_FEMALE_1, UnitIds.villagerwoman)
    ..setGenerics()

    new UnitDefinition(UNITID_FEMALE_2, UnitIds.highelvenfemale)
    ..setGenerics()

    new UnitDefinition(UNITID_MALE_1, UnitIds.villagerman)
    ..setGenerics()

    new UnitDefinition(UNITID_MALE_2, UnitIds.villagerman2)
    ..setGenerics()

    
    

function UnitDefinition.setGenerics()

    this
    ..setAttacksEnabled(0)
    ..setScaleProjectiles(false)
    ..setArmorType(ArmorType.Normal)
    ..setCanFlee(false)
    ..setHideMinimapDisplay(true)
    ..setManaMaximum(0)
    ..setManaInitialAmount(0)
    ..setFoodCost(0)
    ..setGoldBountyAwardedBase(0)
    ..setScalingValue(1.15)
    ..setHitPointsRegenerationRate(0)
    
    
//================================================================================================================================================================================================
// Chat Command
init
    let cmd = defineUtilCommand("testunit")
    ..addOptionInt("n", 1)
    ..addOptionReal("hp", 500)
    ..addOptionReal("armor", 0)
    ..setAction() (p, args, opts) ->
        let count = opts.getInt("n")
        let health = opts.getReal("hp")
        let armor = opts.getReal("armor")
        if( health < 1 )
            p.print("Health 'hp' must be larger than 0")   
        else if( count < 1 )
            p.print("Number of units 'n' must be larger than 0",8)
        else
            let spawnPos = p.getMousePos()
            for i=1 to count
                createTestUnit(spawnPos, health, armor)
    
    cmd.addHelpCommand("Spawns an enemy test unit at your mouse position")
    ..setOptionDescription("n", "Number of units to spawn")
    ..setOptionDescription("hp", "Health of the test units")
    ..setOptionDescription("armor", "Armor of the test units")

