package Game

import public Round 
import Transitions
import FadeAndFilters
import Camera
import EnergySystem
import Music
import TimedSpecialEffects

public unit hero
public constant player ENEMYPLAYER = Player(11)

constant int START_TRAININGPOINTS = 0
constant int START_GOLD             = 200

boolean MAPTESTING = false

boolean escapeIsEnabled = false

public constant vec2 HEROSPAWN_SHOP = vec2(-8129,-8198)
public constant vec2 HEROSPAWN_ARENA = vec2(173,501)

SoundDefinition snd_HeroInit = new SoundDefinition(Sounds.gromWhat3, false)

vec2 spawnPosOnInit

effect teleportEffect


// Game Init --------------------------------------------------------------------------------------------------------------------



init

    stopMusic()
    spawnPosOnInit = (MAPTESTING) ? HEROSPAWN_ARENA : HEROSPAWN_SHOP

    Player(0)
    ..setLumber(START_TRAININGPOINTS)
    ..setGold(START_GOLD)
    ..setCameraPosition(spawnPosOnInit)

    EnableSelect(false, false)
    EnablePreSelect(false, false)
    EnableDragSelect(false, false)

    if not MAPTESTING
        Player(0).setCameraBoundsToRect(gg_rct_Shop_CamBounds)
        
    new Fade
    ..setStartDelay(1)
    ..onFadeIn(function createHero )
    ..start(FADEIN)

    CreateTrigger()
    ..registerPlayerEvent(Player(0), EVENT_PLAYER_END_CINEMATIC)
    ..addAction(function startRound)

    getTimer().start(0.5, function delayedInit)


function createHero()
    hero = createUnit(Player(0), 'O000', spawnPosOnInit, angle(3*PIHALF))
    ..suspendXp(true)
    ..setColor(PLAYER_COLOR_WHEAT)
    EnergySystem.addUnit(hero)
    addEffect("Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl", hero.getPos() )
    ..setDuration(3)

    getTimer().start(2, function playHeroSound)



function playHeroSound()
    GetExpiredTimer().start(2, function enableControls )

    snd_HeroInit.play()
    
    
function enableControls()

    EnableSelect(true, true)
    EnablePreSelect(true, true)
    EnableDragSelect(true, true)

    Player(0)
    ..selectSingle(hero)

    playMusic(Sounds.orcX1)
    escapeIsEnabled = true

    GetExpiredTimer().release()
    

function delayedInit()
    GetExpiredTimer().release()
    
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Shop_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Arena_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_MASKED, GetPlayableMapRect(), false, false).start()


function startRound()

    if escapeIsEnabled

        if not Round.isARoundRunning()
            Round.setARoundRunning()
            stopMusic()
            transitionToArena()
        


