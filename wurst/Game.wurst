package Game

import public Round
import FadeAndFilters
import Camera
import EnergySystem
import Music
import TimedSpecialEffects
import Preloader
import ChargeSystem
import Gladiator
import Dialog
import Damage


constant boolean    SPAWNHEROINARENA        = false
constant boolean    ARENACORNERSPAWN        = false
constant boolean    PREPAREHERO             = false
constant boolean    LOCKCAMBOUNDS           = true
constant boolean    MAKEMAPVISIBLE          = true
constant boolean    CUSTOMROUNDSTART        = false 
constant boolean    STARTATSPECIFICROUND    = true
int        STARTINGROUND                    = 12
constant boolean    INSTANTSPAWN            = false  // Toggle rounds to spawn all units at the same time!
constant boolean    PRINTALPHAINTRO         = false

constant int array STARTINGITEMS = ['I00M', 'I00M','I00M', 'I009']

constant int START_TRAININGPOINTS           = 8
constant int START_GOLD                     = 50


public constant player ENEMYPLAYER = Player(11)
public constant vec2 HEROSPAWN_SHOP = vec2(-8129,-8198)
public constant vec2 HEROSPAWN_ARENA = vec2(173,501)
public constant vec2 HEROSPAWN_ARENACORNER = vec2(600,-100)
public SoundDefinition snd_HeroInit = new SoundDefinition(Sounds.gromWhat3, false)
public SoundDefinition snd_Text = new SoundDefinition(Sounds.questActivateWhat1, false)

public unit         hero
public Gladiator    gladiator


public region MAPREGION
boolean escapeIsEnabled = false
boolean canSkipGameInfo

public timer timer_GameIntro = getTimer()

vec2 spawnPosOnInit

public dialog dialog_RoundPick
public button array dialogButton_RoundPick



// Game Init --------------------------------------------------------------------------------------------------------------------


init
    // Setting up variables
    MAPREGION = CreateRegion()..addRect(GetPlayableMapRect())
    
    playMusic(Sounds.orcX1)
    
    spawnPosOnInit = (SPAWNHEROINARENA) ? ((ARENACORNERSPAWN) ? HEROSPAWN_ARENACORNER: HEROSPAWN_ARENA) : HEROSPAWN_SHOP

    Player(0)
    ..setLumber(START_TRAININGPOINTS)
    ..setGold(START_GOLD)
    
    ..setCameraPosition(gg_rct_Shop_CamBounds.getCenter())

    if SPAWNHEROINARENA
        Player(0).setCameraPosition(gg_rct_Arena_CamBounds.getCenter())
        
    else if LOCKCAMBOUNDS
        Player(0).setCameraBoundsToRect(gg_rct_Shop_CamBounds)
        
        

    Round.toggleInstantSpawn(INSTANTSPAWN)

    

    CreateTrigger()
    ..registerPlayerEvent(Player(0), EVENT_PLAYER_END_CINEMATIC)
    ..addAction(function startRound)


    new Fade
    ..setCinematicMode(true)
    ..setFadeOutDuration(0)
    ..start(FADEOUT)

    getTimer().start(1, function delayedInit)


    if PRINTALPHAINTRO
        timer_GameIntro.start(3.0, function preStartInfo )
    else
        timer_GameIntro.start(3.0, function roundPick )


/** Information give before alpha start */
function preStartInfo()

    snd_Text.play()
    printTimed(  "|cffff9933Welcome to 'Gladiator'|r"+
    
            "\n This map is in |cff99ffccAlpha|r, so features such as "+
            "multiplayer, more Gladiators, more rounds and a score/life system has not been implemented yet."+
            "\n"+
            "\nAny suggestions, bug report or critiscm that you might have are "+
            "appreciated"+
            "\nCatch me on Hiveworkshop.com or lakewc3@gmail.com"+
            "\n"+
            "\nThank you for playing!"+
            "\n- Lake",14)

    timer_GameIntro.start(14, function roundPick )

    

function roundPick()
    if CUSTOMROUNDSTART

        EnableSelect(true, true)

        dialog_RoundPick = createDialog()
        ..setMessage("Choose starting round")
        
        
        for i = 1 to 10 
            dialogButton_RoundPick[i] = dialog_RoundPick.addButton(i.toString(), i)

        dialog_RoundPick.display(Player(0), true)

        CreateTrigger()
        ..registerDialogEvent(dialog_RoundPick)
        ..addAction(function roundPicked)

    else
        gameIntroDelay()


function roundPicked()
    button pressedButton = GetClickedButton()
    
    int round = 1

    for i=1 to 10
        if pressedButton == dialogButton_RoundPick[i]
            round = i
            break

    STARTINGROUND = round

    dialog_RoundPick.display(Player(0), false)

    printTimed("Starting at round "+round.toString(),5)


    timer_GameIntro.start(8, function gameIntroDelay )


function gameIntroDelay()

    timer_GameIntro.start(2, function gameIntro )


function gameIntro()
    snd_Text.play()
    canSkipGameInfo = true
    ClearTextMessages()
    printTimed("|cffffcc00"+"Welcome Gladiator!"+"|r",10)
    printTimed("You will have to face fierce opponents, whose only goal is make you regret the day you "+
       "stepped into the arena. To call yourself the true champion, you have to survive "+Round.getNumberOfRounds().toString()+ " merciless rounds of"+
       " slaughter.",10)
    printTimed("At least you will die with honor.", 10)


    timer_GameIntro.start(13, function fadeIn)


function fadeIn()
    canSkipGameInfo = false

    ClearTextMessages()
    timer_GameIntro.pause()

    new Fade
    ..setEndDelay(1)
    ..onFadeIn(function createHero )
    ..start(FADEIN)


function createHero()


    gladiator = new Gladiator(GladiatorType.ORC, Player(0), spawnPosOnInit, angle(3*PIHALF))

    gladiator.releaseAbility(0)

    // Move all this stuff into Gladiator class

    hero = gladiator.getUnit()
    ..suspendXp(true)
    ..setColor(PLAYER_COLOR_WHEAT)
    ..enableCharge()
    ..setInvulnerable(true)
    ..pause()

    EnergySystem.addUnit(hero)
    
    hero.addArtifactSlots()
    ARTIFACT_BLOODFEATHERSHEART.release()
    
    ITEM_DULLBLADE.release()
    ITEM_LEATHERARMOR.release()
    ITEM_COMMONGEM.release()

    setDamageTestUnit(hero)

    setRound()

    
    
    addEffect("Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl", hero.getPos() )
    ..setDuration(3)

    if PREPAREHERO
        for i = 0 to 4
            hero.addItemById(STARTINGITEMS[i])
            
        

    getTimer().start(1.25, function playHeroSound)



function playHeroSound()
    gladiator.getSoundOnSpawn().play()
    GetExpiredTimer().start(2, function disableCinematicMode )

function disableCinematicMode()
    /*  The Fade object should be able to do this cinematic fading,
        but it hasn't been implemented to enable and disable cinematic
        fading offest from the fade. - Has to be implemented */ 
    ShowInterface(true, 1.5)
    GetExpiredTimer().start(1.5, function enableControls )
    
function enableControls()

    EnableSelect(true, true)
    EnablePreSelect(true, true)
    EnableDragSelect(true, true)

    Player(0)
    ..selectSingle(hero)

    hero
    ..setInvulnerable(false)
    ..unpause()

    GetExpiredTimer().start(3, function printStartInfo)



function printStartInfo()
    snd_Text.play()
    printTimed("Prepare yourself now, and when you're ready, press '"+"|cffffcc00"+"escape|r' to start the round.",5)
    
    GetExpiredTimer().start(8, function printRoundInfo)
    

function printRoundInfo()
    Round.printNextRound()
    escapeIsEnabled = true

function delayedInit()
    GetExpiredTimer().release()
    
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Shop_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Arena_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_MASKED, GetPlayableMapRect(), false, false).start()

    

function setRound()

    LinkedList<Round> rounds = Round.getRounds()

    // for round in rounds
    //     print(round.getTitle())

        

    if STARTATSPECIFICROUND

        
        for round in rounds
            if  round.getNumber() < STARTINGROUND
                round.giveRewardNotFancy()
            else
                break
        destroy rounds
            
        Round.setCurrentRound(STARTINGROUND)


function startRound()

    if escapeIsEnabled

        if not Round.isARoundRunning()
            Round.startNextRound()

    else if canSkipGameInfo
        fadeIn()
        
init
    preloadAbility('A004')
    preloadAbility('A002')
    preloadAbility('A001')
    preloadAbility('A003')
    
    finishPreload()
    


