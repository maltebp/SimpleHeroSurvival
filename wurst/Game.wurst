package Game

import public Round 
import Sound
import Transitions
import TimerUtils
import FadeAndFilters
import Camera

public unit hero
public constant player ENEMYPLAYER = Player(11)

constant int START_TRAININGPOINTS = 0
constant int START_GOLD             = 200

boolean MAPTESTING = true

public constant vec2 HEROSPAWN_SHOP = vec2(-3450,-3580)
public constant vec2 HEROSPAWN_ARENA = vec2(-70,451)

vec2 spawnPosOnInit

effect teleportEffect


// Game Init --------------------------------------------------------------------------------------------------------------------



init

    spawnPosOnInit = (MAPTESTING) ? HEROSPAWN_ARENA : HEROSPAWN_SHOP

    Player(0)
    ..setLumber(START_TRAININGPOINTS)
    ..setGold(START_GOLD)
    ..setCameraPosition(spawnPosOnInit)
    ..disableUserControl()

    if not MAPTESTING
        Player(0).setCameraBoundsToRect(gg_rct_Shop_CamBounds)
        
    new Fade
    ..setStartDelay(1)
    ..onFadeIn(function createHero )
    ..start(FADEIN)

    CreateTrigger()
    ..registerPlayerEvent(Player(0), EVENT_PLAYER_END_CINEMATIC)
    ..addAction(function startRound)

    getTimer().start(0.5, function delayedInit)


function createHero()
    hero = createUnit(Player(0), 'Hmkg', spawnPosOnInit, angle(0))
    ..setLevel(10, false)

    teleportEffect = addEffect("Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl", hero.getPos() )
    getTimer().start(3, function deleteTeleportEffect)
    getTimer().start(2, function enableControls)

function enableControls()
    GetExpiredTimer().release()
    Player(0)
    ..enableUserControl()
    ..selectSingle(hero)

function deleteTeleportEffect()
    GetExpiredTimer().release()
    teleportEffect.destr()

function delayedInit()
    GetExpiredTimer().release()
    
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Shop_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Arena_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_MASKED, GetPlayableMapRect(), false, false).start()

function startRound()

    if not Round.isARoundRunning()
        Round.setARoundRunning()
        transitionToArena()
        


