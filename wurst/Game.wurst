package Game

import public Round
import FadeAndFilters
import Camera
import EnergySystem
import Music
import TimedSpecialEffects
import Preloader
import initlater AttackDamage
import ChargeSystem
import Artifact
import ItemReward
import Gladiator


constant boolean    SPAWNHEROINARENA        = false
constant boolean    PREPAREHERO             = false
constant boolean    LOCKCAMBOUNDS           = true
constant boolean    MAKEMAPVISIBLE          = false
constant boolean    STARTATSPECIFICROUND    = true
constant int        STARTINGROUND           = 2
constant boolean    INSTANTSPAWN            = false  // Toggle rounds to spawn all units at the same time!

constant int START_TRAININGPOINTS           = 8
constant int START_GOLD                     = 50


public constant player ENEMYPLAYER = Player(11)
public constant vec2 HEROSPAWN_SHOP = vec2(-8129,-8198)
public constant vec2 HEROSPAWN_ARENA = vec2(173,501)
public SoundDefinition snd_HeroInit = new SoundDefinition(Sounds.gromWhat3, false)
public SoundDefinition snd_Text = new SoundDefinition(Sounds.questActivateWhat1, false)

public unit hero
public Gladiator gladiator


public region MAPREGION
boolean escapeIsEnabled = false
boolean canSkipGameInfo

public timer timer_GameIntro = getTimer()

vec2 spawnPosOnInit





// Game Init --------------------------------------------------------------------------------------------------------------------



init

    MAPREGION = CreateRegion()..addRect(GetPlayableMapRect())


    playMusic(Sounds.orcX1)

    spawnPosOnInit = (SPAWNHEROINARENA) ? HEROSPAWN_ARENA : HEROSPAWN_SHOP

    Player(0)
    ..setLumber(START_TRAININGPOINTS)
    ..setGold(START_GOLD)
    ..setCameraPosition(gg_rct_Shop_CamBounds.getCenter())

    
    Round.toggleInstantSpawn(INSTANTSPAWN)

    EnableSelect(false, false)
    EnablePreSelect(false, false)
    EnableDragSelect(false, false)

    if LOCKCAMBOUNDS
        Player(0).setCameraBoundsToRect(gg_rct_Shop_CamBounds)

    CreateTrigger()
    ..registerPlayerEvent(Player(0), EVENT_PLAYER_END_CINEMATIC)
    ..addAction(function startRound)

    new Fade
    ..setCinematicMode(true)
    ..setFadeOutDuration(0)
    ..start(FADEOUT)

    getTimer().start(1, function delayedInit)

    timer_GameIntro.start(3.0, function gameIntro )





function gameIntro()
    snd_Text.play()
    canSkipGameInfo = true
    printTimed("|cffffcc00"+"Welcome Gladiator!"+"|r",10)
    printTimed("You will have to face fierce opponents, whose only goal is make you regret the day you "+
       "stepped into the arena. To call yourself the true champion, you have to survive "+Round.getNumberOfRounds().toString()+ " merciless rounds of"+
       " slaughter.",10)
    printTimed("At least you will die with honor.", 10)


    timer_GameIntro.start(13, function fadeIn)


function fadeIn()
    canSkipGameInfo = false

    ClearTextMessages()
    timer_GameIntro.pause()

    new Fade
    ..setEndDelay(1)
    ..onFadeIn(function createHero )
    ..start(FADEIN)


function createHero()


    gladiator = new Gladiator(GladiatorType.ORC, Player(0), spawnPosOnInit, angle(3*PIHALF))

    gladiator.releaseAbility(0)

    // Move all this stuff into Gladiator class

    hero = gladiator.getUnit()
    ..suspendXp(true)
    ..setColor(PLAYER_COLOR_WHEAT)
    ..enableCharge()

    EnergySystem.addUnit(hero)
    
    hero.addArtifactSlots()
    ARTIFACT_WARDRUM.release()
    
    ITEM_DULLBLADE.release()
    ITEM_LEATHERARMOR.release()
    ITEM_COMMONGEM.release()

    setRound()

    
    
    addEffect("Abilities\\Spells\\Human\\MassTeleport\\MassTeleportCaster.mdl", hero.getPos() )
    ..setDuration(3)

    if PREPAREHERO
        hero.addItemById('I00Q')
        hero.addItemById('I00O')
        hero.addItemById('I00N')
        hero.addItemById('I00P')
        

    getTimer().start(1.25, function playHeroSound)



function playHeroSound()
    gladiator.getSoundOnSpawn().play()
    GetExpiredTimer().start(2, function disableCinematicMode )

function disableCinematicMode()
    /*  The Fade object should be able to do this cinematic fading,
        but it hasn't been implemented to enable and disable cinematic
        fading offest from the fade. - Has to be implemented */ 
    ShowInterface(true, 1.5)
    GetExpiredTimer().start(1.5, function enableControls )
    
function enableControls()

    EnableSelect(true, true)
    EnablePreSelect(true, true)
    EnableDragSelect(true, true)

    Player(0)
    ..selectSingle(hero)

    GetExpiredTimer().start(3, function printStartInfo)



function printStartInfo()
    snd_Text.play()
    printTimed("Prepare yourself now, and when you're ready, press '"+"|cffffcc00"+"escape|r' to start the round.",5)
    
    GetExpiredTimer().start(8, function printRoundInfo)
    

function printRoundInfo()
    Round.printNextRound()
    escapeIsEnabled = true

function delayedInit()
    GetExpiredTimer().release()
    
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Shop_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_VISIBLE, gg_rct_Arena_Visibility, false, false).start()
    CreateFogModifierRect(Player(0), FOG_OF_WAR_MASKED, GetPlayableMapRect(), false, false).start()

    // ITEM_DULLBLADE.release()
    // ITEM_LEATHERARMOR.release()
    

    


function setRound()

    if STARTATSPECIFICROUND

        LinkedList<Round> rounds = Round.getRounds()
        for round in rounds
            if  round.getNumber() < STARTINGROUND
                round.giveRewardNotFancy()
            else
                break
        destroy rounds
            
        Round.setCurrentRound(STARTINGROUND)
        print("Editor start: first round is round "+STARTINGROUND.toCharsetString())


function startRound()

    if escapeIsEnabled

        if not Round.isARoundRunning()
            Round.startNextRound()

    else if canSkipGameInfo
        fadeIn()
        
init
    preloadAbility('A004')
    preloadAbility('A002')
    preloadAbility('A001')
    preloadAbility('A003')
    
    finishPreload()
    


