package SoulSiphon

import AbilityCreation
import HoverAbility
import SoundDefinitions
import AttackCooldown
import MyLibrary
import UnitStatsSystem
import DamageDetection
import DamageType
import TrollAnimations
import Projectile
import Assets
import GroupUtils
import Miss
import HeroDefaults
import ComboSystem
import Damage
import BloodPact
import SoulDrainData
import ClosureTimers
import VoodooPotion


let RANGE = DEFAULT_CASTRANGE
let DRAIN_FACTOR = 1. // Equilavent to damage factor
let DRAIN_FACTOR_VOODOO = 1.5
let TRANSFER_FACTOR = 1. // Amount drained gained by the caster
let AIMASSIST = 75.
let CHARGE = 25.


public let TROLL_SOULSIPHON_ID = compiletime( createW( Icons.bTNManaDrain, "Soul Siphon", "Strike an enemy dealing damage to it.")  )

let ABIL_DEF = new PointAbility( TROLL_SOULSIPHON_ID, RANGE, ANIM_TROLL_ATTACK )


init
    ABIL_DEF
    ..queueStandReady()

    ABIL_DEF.setCastCritera() caster ->
        return caster.attemptAttack( SND_SORCERESS_MISSILE_DEATH )

    ABIL_DEF.onEffect() (caster, pos) ->
    
        caster.activateBloodPact()


        let targets = ENUM_GROUP..enumEnemyTargets( caster.getOwner(), pos, AIMASSIST)
        
        let target = targets.getNearestUnit(pos)

        if target != null

            let drainFactor = (target.consumeVoodoo(caster)) ?  DRAIN_FACTOR_VOODOO : DRAIN_FACTOR
            let damage =  caster.getDamage() * drainFactor
            let heal = damage*TRANSFER_FACTOR

            caster.damageTargetSpell( target, damage )
            caster.addComboPoint( COMBO_POINT_W )            

            let projectile = new Projectile(target.getPos().withTerrainZ(60), Abilities.zigguratMissile)
            ..fireUnit( caster, 60, true, 1000, 0)

            projectile
            ..addActionOnFinish() ->
                if caster != null and caster.isAlive()
                    caster.setHP( caster.getHP()+heal)
                    doAfter(0.01) ->
                        caster.addCharge(30)
                

            SND_POSSESSION.playOnPoint(pos.withTerrainZ())
        else
            SND_SPELL_MISS.playOnPoint(caster.getPos3Real())
            caster.missAttack()

        targets.clear()
