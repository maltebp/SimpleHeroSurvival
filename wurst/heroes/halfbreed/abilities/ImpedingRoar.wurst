package ImpedingRoar


import AbilityTools

//================================================================================================================================================================================================
// Settings

let CHARGE          = 30
let DMG_FACTOR      = 0.25
let AOE_RANGE       = 300.
let ATTACKSPEED_RED = 0.20
let MOVESPEED_RED   = 0.3
let DURATION        = 10.


public let HALF_BREED_IMPEDING_ROAR = compiletime(defineImmediate1(
    "Impeding Roar",
    "Attempt at executing an enemy, dealing {0} of you power in damage. If the execution is succesful, the cooldown of the ability will be reset",
    Icons.bTNMaskOfDeath,
    "D",
    12,         // Cooldown
    10,         // Mana
    "spell,slam"
))

public let HALF_BREED_IMPEDING_ROAR_BUFF = compiletime(defineBuff(
    "Impeding Roar",
    "Movement and attack speed is slowed.",
    Icons.bTNMaskOfDeath,
    Abilities.stasisTotemTarget,
    "overhead",
    ""
))

let SND_ON_ATTACK = [
    new SoundDefinition(Sounds.metalHeavyBashMetal1, false, true),
    new SoundDefinition(Sounds.metalHeavyBashMetal2, false, true),
    new SoundDefinition(Sounds.metalHeavyBashMetal3, false, true)
]

let SND_ON_ATTACK_2 = new SoundDefinition(Sounds.shockwave, false, true)

let SND_ROAR = new SoundDefinition(Sounds.howlOfTerror, false, true)



//================================================================================================================================================================================================

init
    registerSpellEffectEvent(HALF_BREED_IMPEDING_ROAR) ->
        onCast()

function onCast()
    let caster = GetSpellAbilityUnit()

    // Adjust charge
    caster.adjustCharge(CHARGE)
    
    caster.addEffect(Abilities.roarCaster, "origin")..setDuration(5)
    SND_ROAR.playOnPoint(caster.getPos().withTerrainZ())

    let damage = caster.getDamage() * DMG_FACTOR

    let targets = enemiesInRange(caster.getOwner(), caster.getPos(), AOE_RANGE)
    for target from targets
        caster.damageTargetPhysical(target, damage)
        target.addEffect(Abilities.spellStealMissile, "chest").destr()
        target.applyBuff(new ImpedingRoarBuff(), caster, DURATION)


class ImpedingRoarBuff extends Buff

    construct()
        super(HALF_BREED_IMPEDING_ROAR_BUFF, false)
        
    override function onApply()
        if( target != null )
            target.adjustStatPercentage(STAT_MOVESPEED, -MOVESPEED_RED) 
            target.adjustStatPercentage(STAT_ATTACKSPEED, -ATTACKSPEED_RED) 

    override function onRemove()
        if( target != null )
            target.adjustStatPercentage(STAT_MOVESPEED, MOVESPEED_RED) 
            target.adjustStatPercentage(STAT_ATTACKSPEED, ATTACKSPEED_RED) 
            