// ================================================================================================================================

package Gladiator

// -----------------------------------------------------------------------------------------------------------------------------

import          LinkedList
import public   SoundUtils
import          ErrorHandling
import ComboSystem
import ClearPoints
import ChargeSystem
import UnitStatsSystem
import initlater Artifact



// ================================================================================================================================
public class Gladiator
    private static LinkedList<thistype> instances = new LinkedList<thistype>

    private unit            gladiatorUnit
    private GladiatorType   gladiatorType
    private player          owner


    construct(GladiatorType whichType, player owner, vec2 spawnPos, angle facing )
        this.owner = owner
        gladiatorType = whichType

        gladiatorUnit = gladiatorType.newGladiatorUnit(owner, spawnPos, facing)

        let comboUnitDefinition = ComboUnitDefinition.getDefinition(gladiatorUnit)

        gladiatorUnit.addAbility(comboUnitDefinition.getListAbil())
        
        instances.add(this)


    /** Releases an ability, adding it to the gladiator unit. The abilityNumber is the number in the list */
    function releaseAbility(int abilityNumber) returns string
        int abilityId = gladiatorType.getAbility(abilityNumber)
        
        if gladiatorUnit.getAbilityLevel(abilityId) > 0
            error(gladiatorUnit.getProperName()+" already has that ability")

        else
            gladiatorUnit.addAbility(abilityId)

        return GetAbilityName(abilityId)


    function getUnit() returns unit
        return gladiatorUnit


    function getSoundOnSpawn() returns SoundDefinition
        return gladiatorType.getSoundOnSpawn()


    function getSoundOnRespawn() returns SoundDefinition
        return gladiatorType.getSoundOnRespawn()


    function getSoundOnNewAbility() returns SoundDefinition
        return gladiatorType.getSoundOnNewAbility()

    
    function getOwner() returns player
        return owner

    function getColor() returns string
        return gladiatorType.getColor()

    function getGladiatorType() returns GladiatorType
        return gladiatorType
    

    static function isUnitGladiator(unit whichUnit) returns boolean
        return getInstance(whichUnit) != null

    static function getInstance(unit  whichUnit) returns Gladiator
        for instance in instances
            if instance.gladiatorUnit == whichUnit
                return instance
        return null

    static function getInstance(player whichPlayer) returns Gladiator
        for instance in instances
            if instance.owner == whichPlayer
                return instance
        return null




// ================================================================================================================================
interface GladiatorAction
    function run(unit gladiator)

public class GladiatorType

    private static let allTypes = new LinkedList<thistype>

    private static boolean hasBeenInitialized

    private int unitType

    private string name

    private string color

    private real damage = 0
    private real armor = 0
    private real moveSpeed = 0
    private real attackCooldown = 0
    private real health = 0

    protected int QAbility
    protected int WAbility
    protected int EAbility

    private let abilities = new LinkedList<int>
    private let artifacts = new LinkedList<Artifact>

    private SoundDefinition snd_Spawn = null
    private SoundDefinition snd_Respawn = null
    private SoundDefinition snd_NewAbility = null
    
    private LinkedList<GladiatorAction> actions_OnSpawn = null

    construct()
        allTypes.add(this)

    
    function setUnitType(int unitType)
        this.unitType = unitType

        unit tempUnit = createUnit(Player(PLAYER_NEUTRAL_PASSIVE), unitType, vec2(0,0),angle(0))
        name = tempUnit.getName()
        tempUnit.remove()


    function setBaseStats( real damage, real armor, real health, real attackCooldown, real moveSpeed  )
        this.damage = damage
        this.armor = armor
        this.health = health
        this.attackCooldown = attackCooldown
        this.moveSpeed = moveSpeed

    function setSoundOnSpawn(string soundPath)
        snd_Spawn = new SoundDefinition(soundPath, false)


    function setSoundOnRespawn(string soundPath)
        snd_Respawn = new SoundDefinition(soundPath, false)


    function setSoundOnNewAbility(string soundPath)
        snd_NewAbility = new SoundDefinition(soundPath, false)
    

    function setColor(string color)
        this.color = color

    function addAbilities(vararg int abilities)
        for abil in abilities
            this.abilities.add(abil)


    function getSoundOnSpawn() returns SoundDefinition
        checkInit()
        return snd_Spawn 


    function getSoundOnRespawn() returns SoundDefinition
        checkInit()
        return snd_Respawn


    function getSoundOnNewAbility() returns SoundDefinition
        checkInit()     
        return snd_NewAbility


    function getAbility(int number) returns int
        checkInit()
        return abilities.get(number)         
    

    function getAllAbilities() returns LinkedList<int>
        checkInit() 
        return null


    function getUnitType() returns int
        checkInit() 
        return unitType

    function getColor() returns string
        checkInit()
        return color

    function setBaseAbilities( int q, int w, int e)
        QAbility = q
        WAbility = w
        EAbility = e


    function addActionOnSpawn( GladiatorAction action )
        if actions_OnSpawn == null
            actions_OnSpawn = new LinkedList<GladiatorAction>
        actions_OnSpawn.add(action)

    private static function checkInit()
        if not hasBeenInitialized
            initialize()

    function newGladiatorUnit( player owner, vec2 spawnPos, angle facing) returns unit
        unit gladiatorUnit = createUnit(owner, unitType, spawnPos, facing)
        ..addAbility(QAbility)
        ..addAbility(WAbility)
        ..addAbility(EAbility)
        ..addAbility(CLEAR_POINTS_ABIL_ID)
        ..suspendXp(true)
        ..enableCharge()
        ..setBaseDamage(damage)
        ..setBaseAttackCooldown(attackCooldown)
        ..setMana(attackCooldown*100)
        ..addArtifactSlots()

         // These are not implemented in the unit stat system yet
        gladiatorUnit
        ..setArmor(armor)
        ..setMoveSpeed(moveSpeed)
        ..setMaxHP( health.toInt(), true)

        if actions_OnSpawn != null
            for action in actions_OnSpawn
                action.run(gladiatorUnit)
    
        return gladiatorUnit

        
    function getName() returns string
        return name

    function addArtifact( Artifact artifact)
        artifacts.add(artifact)

    function releaseNextArtifact() returns Artifact
        for artifact in artifacts
            if not artifact.isReleased()
                artifact.release()
                return artifact
        return null

    function unlockComboMove() returns string
        return ComboUnitDefinition.getDefinition(unitType).unlockMove().getName()

    static function getAllTypes() returns LinkedList<thistype>
        return allTypes
        
// ================================================================================================================================
// GLADIATOR TYPE SETUP

    private static function initialize()
        hasBeenInitialized = true

                    
    

    
        



        
        
